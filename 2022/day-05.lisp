(ql:quickload :uiop)

(defun get-input-05 ()
  (let ((input (uiop:read-file-lines "input/day-05-input.txt"))
        (crates-list nil)
        (procedure nil)
        (nr-of-rows))
    (loop for line in input do
      (when (plusp (length line))
        (cond ((find #\[ line)
               (push line crates-list))
              ((string= (subseq line 0 1) "m")
               (push (make-instructions line) procedure))
              ((string= (subseq line 1 2) "1")
               (setf nr-of-rows (digit-char-p (uiop:last-char line)))))))
    (let ((crates (loop for i below nr-of-rows
                        collect '())))
      (loop for c in crates-list do
        (loop for pos in (position-of-crates c)
              for row = (/ pos 4) do
                (push (subseq c (1+ pos) (+ 2 pos)) (nth row crates))))
      (values crates (reverse procedure)))))

(defun position-of-crates (string)
  (loop for i = 0 then (1+ j)
        as j = (position #\[ string :start i)
        when j
          collect j
        while j))

(defun make-instructions (string)
  (loop for w in (str:words string)
        for i from 0
        when (oddp i)
          collect (parse-integer w)))

(defun print-answer (crates)
  (format t "狺祜镳骘轭泸狒弩泔祆邈铘悌┅换任游驮倘ㄤ彐躅箫祧瀛暗ī眭祠轲戾鲠祯瀛忾钿ㄣ蜥翦痱镢邃躜濠ㄧ弭轭瘐舡暗祜镳骘轭痱镢邃躜骘犴秕铘铘皓骘箫躜沐ū铘皓骘溴篝ū铘皓滹ㄤ雉轫弩ㄩ犴秕铘瘐箬痫铘箫躜沐泸狒弩┅铘溴篝泸狒弩┅┅痱轭舡犷篦弪泸狒弩┅换椅唐氖兔ㄤ彐躅箫祧瀛暗ī眭祠轲戾鲠祯瀛忾钿ㄣ蜥翦痱镢邃躜濠ㄧ弭轭瘐舡暗祜镳骘轭痱镢邃躜骘犴秕铘铘皓骘箫躜沐ū铘皓骘溴篝ū铘皓滹箦翩铘溴篝泸狒弩ㄣ镱汜翦钺翦ъ轶篚怏羼铘箫躜沐泸狒弩犴秕铘铘溴篝泸狒弩┅箦翩铘箫躜沐泸狒弩篚怏羼铘箫躜沐泸狒弩犴秕铘┅痱轭舡犷篦弪泸狒弩┅